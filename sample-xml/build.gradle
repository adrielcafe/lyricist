apply plugin: "com.android.library"
apply plugin: "com.google.devtools.ksp"
apply from: "../android-module.gradle"

android {
    namespace "cafe.adriel.lyricist.sample.xml"
}

ksp {
    arg("lyricist.xml.moduleName", "xml")
    arg("lyricist.xml.defaultLanguageTag", "en")
    arg("lyricist.xml.resourcesPath", android.sourceSets.main.res.srcDirs.first().absolutePath)
}

// Fix for KSP 2.0 task dependencies and race conditions in Android projects
// Based on: https://github.com/google/ksp/issues/1940
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    if (name.contains("ksp")) return
    dependsOn tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).matching {
        it.name.startsWith("ksp") && it.name.endsWith("Kotlin")
    }
}

dependencies {
    implementation(project(":lyricist-compose"))
    ksp(project(":lyricist-processor-xml"))

    implementation libs.compose.runtime
    implementation libs.compose.ui.text
}